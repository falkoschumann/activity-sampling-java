import java.time.LocalDate

plugins {
  id 'activitysampling.groovy-gradle-conventions'
  id 'checkstyle'
  id 'jacoco'
  id 'java'
  id 'jvm-test-suite'
  id 'com.diffplug.spotless'
  id 'io.freefair.lombok'
  id 'org.sonarqube'
}

version = rootProject.version

ext {
  copyrightYear = LocalDate.now().year
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
  // TODO remove dependency from Mockito
  testImplementation 'org.mockito:mockito-core:4.9.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'
}

compileJava {
  options.release = 17
  options.encoding = 'utf-8'
  options.compilerArgs.addAll(['-Werror', '-implicit:class'])
}

tasks {
  processResources {
    filesMatching('**/*.properties') {
      expand([version: version, copyrightYear: copyrightYear])
    }
  }
}

test {
  systemProperty 'user.country', 'DE'
  systemProperty 'user.language', 'de'
  systemProperty 'user.timezone', 'Europe/Berlin'
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
    showExceptions = true
    exceptionFormat = 'full'
  }
  finalizedBy jacocoTestReport
}

compileTestJava {
  options.release = 17
  options.encoding = 'utf-8'
  options.compilerArgs.addAll(['-Werror', '-implicit:class'])
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

checkstyle {
  var archive = configurations.checkstyle.filter {
    it.name.startsWith("checkstyle")
  }
  config = resources.text.fromArchiveEntry(archive, "google_checks.xml")
  configProperties 'org.checkstyle.google.suppressionfilter.config': file("${project.rootDir}/config/checkstyle/suppressions.xml")
  maxWarnings = 0
}

spotless {
  java {
    googleJavaFormat()
    licenseHeaderFile 'config/LicenseHeader.txt'
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "falkoschumann_activity-sampling-java"
    property "sonar.organization", "falkoschumann"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
