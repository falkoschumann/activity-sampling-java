import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'org.beryx.jlink'
}

jlink {
  options = [
    '--compress', '2',
    '--include-locales', 'en',
    '--no-header-files',
    '--no-man-pages',
    '--strip-native-commands',
    '--strip-debug',
  ]
  jpackage {
    imageOptions += [
      '--vendor', 'Muspellheim',
    ]
    installerOptions = [
      '--license-file', "$rootDir/LICENSE.txt",
    ]
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/macos/app-icon.icns"
      imageOptions += [
        '--copyright', "Copyright Â© ${copyrightYear} Falko Schumann",
        '--mac-sign',
        '--mac-signing-key-user-name', 'Falko Schumann (QC6EN37P56)',
        '--resource-dir', "$projectDir/src/main/macos",
      ]
      installerType = 'dmg'
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/win/app-icon.ico"
      imageOptions = [
        '--copyright', "Copyright (c) ${copyrightYear} Falko Schumann",
        '--resource-dir', "$projectDir/src/main/win",
      ]
      installerType = 'msi'
      installerOptions = [
        '--copyright', "Copyright (c) ${copyrightYear} Falko Schumann",
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
      ]
    }
  }
}
