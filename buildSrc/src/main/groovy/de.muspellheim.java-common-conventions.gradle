import java.time.LocalDate

plugins {
  id 'jacoco'
  id 'java'
  id 'jvm-test-suite'
  id 'com.diffplug.spotless'
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly 'org.projectlombok:lombok:1.18.24'
  annotationProcessor 'org.projectlombok:lombok:1.18.24'
  testCompileOnly 'org.projectlombok:lombok:1.18.24'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
  testImplementation 'org.mockito:mockito-core:4.9.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'
}

version = rootProject.version

ext {
  copyrightYear = LocalDate.now().year
}

testing {
  suites {
    test {
      useJUnitJupiter()
    }
  }
}

test {
  systemProperty "user.country", "DE"
  systemProperty "user.language", "de"
  systemProperty "user.timezone", "Europe/Berlin"
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
}

spotless {
  java {
    googleJavaFormat()
  }
}
